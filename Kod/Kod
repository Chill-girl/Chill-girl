#include <Arduino.h>
#include <Wire.h>
#include <LiquidCrystal_I2C.h> // Бібліотека для роботи з LCD по I2C.
#include <microDS18B20.h>     // Бібліотека для роботи з датчиком температури

LiquidCrystal_I2C lcd(0x27, 20, 4); // Ініціалізація дисплея LCD 2004
MicroDS18B20<2> sensor;             // Пін підклюення датика температури

float calibration_value = 21.34;
const uint8_t phPin = A1;  // Пін підключення аналогового вихіда модуля

// TDS константи
const uint8_t pin_Vm = A0;  // Пін підключення аналогового вихіда модуля

const float Vcc  = 5.0f;    // Напруга Arduino
const float Vccm = 3.3f;    // Напруга ОУ модуля
const float Ka = 1000.0f; // Множитель ступеневої функції (визначається
                            // калібр. модуля). Можна отримати функцією getKa()
const float Kb = -5.0f;   // Степень ступеневої функції (визнач.калібруванням
                            // модуля). Можно отримати функцієй getKb()
const float Kt   = 0.02f;   // Темп. коеф-нт (залежить від складу рідини)
const float Kp   = 0.5f;    // Коеф. пересчёта (залежить від состава рідини)
const float Kf   = 0.85f;   // Коефіцієнт передачі ФВЧ+ФНЧ модуля
                            // (залежить від частоти змінного потоку, 
                            // використовуваного для проведення вимірювань)
const float T    = 25.0f;   // Опорна температура в °C
const float t    = 25.0f;   // Температура рідини в °C

void setup()
 {
  lcd.init();                      // Ініціалізуємо дисплей.
  lcd.backlight();                 // Вмикаємо підсвітку.  
  lcd.setCursor(0, 0);             // Курсор на першу колонку, 1 рядок.
  lcd.print("Hello");        // Виводимо текст.
  lcd.setCursor(0, 1);             // Курсор на першу колонку, 2 рядок.
  lcd.print("Mala Akademiya nauk!");       // Виводимо текст.
  delay(10000);                    // Спимо 10 секунд
}

void loop()                        // цикліна функція 
{
  tdsMeter();                      // виклик функції опитування TDS датчика
  phSensor();                      // виклик функції опитування PH датчика
  tSensor();                       // виклик функції опитування температури
}

void tdsMeter()                    // Функція опитування TDS датчика
{
  // збір даних
  float Vm  = analogRead(pin_Vm)*Vcc/1023;    // Отримання напруги (В)
  float S   = Ka*pow((Vccm-Kf*Vm)/2,Kb)/1000; // питома електропр-сть мСм/см
  float EC  = S / (1+Kt*(t-T));               // Приводимо роздільну електр-сть
                                              // рідини до опорної T. (мСм/см)
  float TDS = EC * Kp;                        // Мінераліцація рідини (ppm)

  // вивод даних
  lcd.clear();                                // Очищаємо весь дисплей
  lcd.setCursor(0, 0);
  lcd.print("TDS Meter:");
  lcd.setCursor(0, 1);
  lcd.print((String)"S   = " + S + "mSM/sm");
  lcd.setCursor(0, 2);
  lcd.print((String)"ES  = " + EC + "mSM/sm");
  lcd.setCursor(0, 3);
  lcd.print((String)"TDS = " + TDS + "ppm");
  delay(10000);
}

void phSensor()               // Функція опитування PH датчика
{
  int buf[10], temp;
  for(int i=0; i<10; i++) {   // Get 10 sample value from sensor for smooth value
    buf[i] = analogRead(phPin);
    delay(10);
  }

  for(int i=0; i<9; i++) {     // booble sort
    for(int j=i+1; j<10; j++) {
      if(buf[i] > buf[j]) {
        temp = buf[i];
        buf[i] = buf[j];
        buf[j] = temp;
      }
    }
  }
  float avgValue = 0;
  for(int i=2; i<8; i++)
    avgValue += buf[i];             //take the average value of 6 center sample
    
  float ph = (float)avgValue * 5.0 / 1024 / 6; //convert analog into millivolt
  ph = -5.70 * ph + calibration_value;         //convert the millivolt into pH

  // вивод даних
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("PH Sensor:");
  lcd.setCursor(0, 1);
  lcd.print((String)"PH = " + ph);
  lcd.setCursor(0, 2); 

  if(ph<4)                 lcd.print(" Duzhe kyslyi ");
  else if(ph>=4  && ph<5)  lcd.print("   Kyslyi   ");
  else if(ph>=5  && ph<7)  lcd.print(" Kysluvatyi ");
  else if(ph>=7  && ph<8)  lcd.print("   Neitralnyi  ");
  else if(ph>=8  && ph<10) lcd.print("Luzhnuvatyi ");
  else if(ph>=10 && ph<11) lcd.print("  Luzhnyi   ");
  else if(ph>=11)          lcd.print("Duzhe luzhnyi");

  delay(10000);
}

void tSensor()                     // Функція опитування температури
{
  sensor.requestTemp();            // зчитати температуру
  float t = sensor.getTemp();      // отримати температуру

  // вивод даних
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Temperature Sensor:");
  lcd.setCursor(0, 1);
  lcd.print((String)"T = " + t);
  lcd.write(223);                 // символ градусів цельсія
  delay(10000);
}
